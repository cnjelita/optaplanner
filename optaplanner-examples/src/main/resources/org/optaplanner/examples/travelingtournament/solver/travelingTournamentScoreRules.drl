/*
 * Copyright 2010 JBoss Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.optaplanner.examples.travelingtournament.solver;
    dialect "java"

import org.optaplanner.core.api.score.buildin.hardsoft.HardSoftScoreHolder;
import org.optaplanner.core.impl.score.constraint.IntConstraintOccurrence;
import org.optaplanner.core.impl.score.constraint.ConstraintType;

import org.optaplanner.examples.travelingtournament.domain.Match;
import org.optaplanner.examples.travelingtournament.domain.Day;
import org.optaplanner.examples.travelingtournament.domain.Team;
import org.optaplanner.examples.travelingtournament.domain.TravelingTournament;

global HardSoftScoreHolder scoreHolder;

// ############################################################################
// Hard constraints
// ############################################################################

// multipleMatchesPerTeamPerDay is obsolete because it is always 0

rule "fourConsecutiveHomeMatches"
    when
        $m : Match($homeTeam : homeTeam, $day1 : day)
        Match(homeTeam == $homeTeam, eval(day.getIndex() == $day1.getIndex() + 1))
        Match(homeTeam == $homeTeam, eval(day.getIndex() == $day1.getIndex() + 2))
        Match(homeTeam == $homeTeam, eval(day.getIndex() == $day1.getIndex() + 3))
    then
        insertLogical(new IntConstraintOccurrence("fourConsecutiveHomeMatches", ConstraintType.HARD,
                -1,
                $m));
end

rule "fourConsecutiveAwayMatches"
    when
        $m : Match($awayTeam : awayTeam, $day1 : day)
        Match(awayTeam == $awayTeam, eval(day.getIndex() == $day1.getIndex() + 1))
        Match(awayTeam == $awayTeam, eval(day.getIndex() == $day1.getIndex() + 2))
        Match(awayTeam == $awayTeam, eval(day.getIndex() == $day1.getIndex() + 3))
    then
        insertLogical(new IntConstraintOccurrence("fourConsecutiveAwayMatches", ConstraintType.HARD,
                -1,
                $m));
end

rule "matchRepeater"
    when
        $m : Match($homeTeam : homeTeam, $awayTeam : awayTeam, $day1 : day)
        Match(homeTeam == $awayTeam, awayTeam == $homeTeam, eval(day.getIndex() == $day1.getIndex() + 1))
    then
        insertLogical(new IntConstraintOccurrence("matchRepeater", ConstraintType.HARD,
                -1,
                $m));
end

// ############################################################################
// Soft constraints
// ############################################################################

rule "startToAwayHop"
    when
        $m : Match($toTeam : homeTeam, $team : awayTeam, $day : day)
        not Day(eval(index == $day.getIndex() - 1))
    then
        insertLogical(new IntConstraintOccurrence("startToAwayHop", ConstraintType.SOFT,
                - $team.getDistance($toTeam),
                $m));
end
rule "homeToAwayHop"
    when
        $m1 : Match($team : homeTeam, $day1 : day)
        $m2 : Match($toTeam : homeTeam, awayTeam == $team, eval(day.getIndex() == $day1.getIndex() + 1))
    then
        insertLogical(new IntConstraintOccurrence("homeToAwayHop", ConstraintType.SOFT,
                - $team.getDistance($toTeam),
                $m1, $m2));
end
rule "awayToAwayHop"
    when
        $m1 : Match($fromTeam : homeTeam, $team : awayTeam, $day1 : day)
        $m2 : Match($toTeam : homeTeam, awayTeam == $team, eval(day.getIndex() == $day1.getIndex() + 1))
    then
        insertLogical(new IntConstraintOccurrence("awayToAwayHop", ConstraintType.SOFT,
                - $fromTeam.getDistance($toTeam),
                $m1, $m2));
end
rule "awayToHomeHop"
    when
        $m1 : Match($fromTeam : homeTeam, $team : awayTeam, $day1 : day)
        $m2 : Match(homeTeam == $team, eval(day.getIndex() == $day1.getIndex() + 1))
    then
        insertLogical(new IntConstraintOccurrence("awayToHomeHop", ConstraintType.SOFT,
                - $fromTeam.getDistance($team),
                $m1, $m2));
end
rule "awayToEndHop"
    when
        $m : Match($fromTeam : homeTeam, $team : awayTeam, $day : day)
        not Day(eval(index == $day.getIndex() + 1))
    then
        insertLogical(new IntConstraintOccurrence("awayToEndHop", ConstraintType.SOFT,
                - $fromTeam.getDistance($team),
                $m));
end

// ############################################################################
// Calculate score
// ############################################################################

// Accumulate hard constraints
rule "accumulateHardScore"
        salience -1 // Do the other rules first (optional, for performance)
    when
        $hardTotal : Number() from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.HARD, $weight : weight),
            sum($weight) // Vote for http://jira.jboss.com/jira/browse/JBRULES-1075
        )
    then
        scoreHolder.setHardScore($hardTotal.intValue());
end

// Accumulate soft constraints
rule "accumulateSoftScore"
        salience -1 // Do the other rules first (optional, for performance)
    when
        $softTotal : Number() from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.SOFT, $weight : weight),
            sum($weight) // Vote for http://jira.jboss.com/jira/browse/JBRULES-1075
        )
    then
        scoreHolder.setSoftScore($softTotal.intValue());
end

